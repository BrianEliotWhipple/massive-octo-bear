plugins {
    id 'java'
    id  'com.github.johnrengelman.shadow' version '1.2.1'
}

project.ext.sourceCompatibility = 1.8
project.ext.targetCompatibility = 1.8

version = '0.1'

repositories {
    jcenter()
}

sourceSets {
    dagger {
        java {
            srcDirs = ['src/dagger/java']
        }
    }
}

configurations {
    compileDagger
}

compileJava {
    description = "dagger annotation processor is loaded automatically from classpath"
    sourceSets.dagger.java.srcDirs*.mkdirs()
    classpath += configurations.compileDagger
    options.compilerArgs += [
            '-s', sourceSets.dagger.java.srcDirs.iterator().next()
    ]
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.8.1'
    compile 'com.datastax.cassandra:cassandra-driver-core:2.1.5'
    compile 'com.google.dagger:dagger:2.0'
    compileDagger 'com.google.dagger:dagger-compiler:2.0'
}

jar {
    manifest {
        attributes("Implementation-Title": "Gradle",
                "Implementation-Version": version,
                'Main-Class': 'com.example.echo.EchoApplication')
    }
}

shadowJar {
    mergeServiceFiles('META-INF/services')
}
